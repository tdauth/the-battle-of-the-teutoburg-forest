function InitHero takes nothing returns nothing
    if (GetUnitCount(VARUS) > 0) then
        set hero_id = VARUS
        set skills1[ 1] = HOLY_BOLT
        set skills1[ 2] = COMMAND_AURA
        set skills1[ 3] = SHOCKWAVE
        set skills1[ 4] = COMMAND_AURA
        set skills1[ 5] = SHOCKWAVE
        set skills1[ 6] = REINCARNATION
        set skills1[ 7] = COMMAND_AURA
        set skills1[ 8] = SHOCKWAVE
        set skills1[ 9] = HOLY_BOLT
        set skills1[10] = HOLY_BOLT

        call BlzDisplayChatMessage(Player(GetAiPlayer()), 0, "I play Varus.")
    elseif (GetUnitCount(TRIBUNE) > 0) then
        set hero_id = TRIBUNE
        set skills1[ 1] = SCOUT
        set skills1[ 2] = DEVOTION_AURA
        set skills1[ 3] = DIVINE_SHIELD
        set skills1[ 4] = DEVOTION_AURA
        set skills1[ 5] = DIVINE_SHIELD
        set skills1[ 6] = VOLCANO
        set skills1[ 7] = DEVOTION_AURA
        set skills1[ 8] = DIVINE_SHIELD
        set skills1[ 9] = SCOUT
        set skills1[10] = SCOUT

        call BlzDisplayChatMessage(Player(GetAiPlayer()), 0, "I play a tribune.")
    else
        set hero_id = SENATOR
        set skills1[ 1] = SILENCE
        set skills1[ 2] = BRILLIANCE_AURA
        set skills1[ 3] = SLEEP
        set skills1[ 4] = BRILLIANCE_AURA
        set skills1[ 5] = SLEEP
        set skills1[ 6] = CHARM
        set skills1[ 7] = BRILLIANCE_AURA
        set skills1[ 8] = SLEEP
        set skills1[ 9] = SILENCE
        set skills1[10] = SILENCE

        call BlzDisplayChatMessage(Player(GetAiPlayer()), 0, "I play a senator.")
    endif
endfunction

function AttackTarget takes unit target, boolean addAlliance returns nothing
    if (target == null) then
        call Trace("Attack target but have nothing\n")
        return
    endif
    if (addAlliance) then
        call Trace("Attack target with alliance target\n")
        call SetAllianceTarget(target)
    endif
    call Trace("Attack target form a group and move to kill\n")
    call FormGroup(3, true)
    call AttackMoveKillA(target)
    if (not addAlliance) then
        call SetAllianceTarget(null)
    endif
endfunction

function LaunchAttack takes nothing returns nothing
    local unit target = null
    local boolean setAlly = true

    call Trace("Launch attack\n")

    // Don't launch any attack while town is threatened
    if (TownThreatened()) then
        call Trace("Launch attack cancel since town is threatened\n")
        call Sleep(2)
        return
    endif

    call Trace("Launch attack enemy player\n")
    // Target Priority #1
    if (target == null) then
        set target = GetAllianceTarget()
        call Trace("Launch attack alliance target\n")
        if (target != null) then
            call Trace("Launch attack alliance target found\n")
            set setAlly = false
        endif
    endif

    // Target Priority #2
    if (target == null) then
        set target = GetExpansionFoe()
        call Trace("Launch attack expansion foe\n")
        if (target != null) then
            call Trace("Launch attack expansion foe found\n")
            set take_exp = false
        endif
    endif

    // Target Priority #3
    if (target == null) then
        call Trace("Launch attack mega target\n")
        set target = GetMegaTarget()
    endif

    // Target Priority #4
    if (target == null) then
        call Trace("Launch attack enemy expansion\n")
        set target = GetEnemyExpansion()
    endif

    // Target Priority #5
    if (target == null) then
        call Trace("Launch attack enemy base\n")
        call StartGetEnemyBase()
        loop
            exitwhen (not WaitGetEnemyBase())
            call SuicideSleep(1)
        endloop
        set target = GetEnemyBase()
    endif

    // Target Priority #6
    if (target == null) then
        call Trace("Launch attack creep camp small\n")
        set target = GetCreepCamp(0, 9, false)
    endif

    // Target Priority #7
    if (target == null) then
        call Trace("Launch attack creep camp big\n")
        set target = GetCreepCamp(0, 75, true)
    endif

    // Attack the target and increment attack wave
    if (target != null) then
        call Trace("Attack\n")
        call AttackTarget(target, setAlly)
    else
        call Trace("Wait for another attack try\n")
        // If no target was found, sleep a bit before trying again
        call Sleep(20)
    endif
endfunction

function main takes nothing returns nothing
    call InitHero()

    call CampaignAI(ROMAN_HOUSE, function SkillArrays)
    call SetReplacements(9,9,9)
    call SetPeonsRepair(true)
    call SetSlowChopping(false)
    call SetWoodPeons(7)

    call SetBuildUnitEx( 1,1,1, ROMAN_TIER_1    )
    call SetBuildUnitEx( 12,12,12, ROMAN_WORKER         )
    call SetBuildUnitEx( 1,1,1, ROMAN_BARRACKS    )
    call SetBuildUnitEx( 1,1,1, ROMAN_MILL    )
    call SetBuildUnitEx( 1,1,1, ROMAN_ALTAR    )
    call SetBuildUnitEx( 1,1,1, ROMAN_GUARD_TOWER    )
    call SetBuildUnitEx( 1,1,1, ROMAN_SHOP    )

    call SetBuildUnitEx( 1,1,1, ROMAN_TIER_2    )
    call SetBuildUnitEx( 1,1,1, ROMAN_TEMPLE    )
    call SetBuildUnitEx( 1,1,1, ROMAN_WORKSHOP    )

    call SetBuildUnitEx( 1,1,1, ROMAN_TIER_3    )

    call SetBuildUnitEx( 1,1,1, ROMAN_STABLES    )

    loop
        call InitAssaultGroup()
        call CampaignAttackerEx(1, 1, 1, hero_id)
        call CampaignAttackerEx(3, 4, 4, ROMAN_LEGIONARY)
        call CampaignAttackerEx(1, 2, 3, ROMAN_ARCHER)
        call CampaignAttackerEx(1, 2, 3, ROMAN_SLINGER)
        call CampaignAttackerEx(1, 2, 3, ROMAN_EQUES)
        call Sleep(M1) // Waits 1 minute before attacking
        call LaunchAttack()
    endloop
endfunction
