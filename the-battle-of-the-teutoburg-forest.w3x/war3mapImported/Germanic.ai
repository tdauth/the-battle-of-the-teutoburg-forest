function InitHero takes nothing returns nothing
    if (GetUnitCount(ARMINIUS) > 0) then
        set hero_id = ARMINIUS
    elseif (GetUnitCount(CHIEFTAIN) > 0) then
        set hero_id = CHIEFTAIN
    else
        set hero_id = DRUID
    endif
endfunction

function AttackTarget takes unit target, boolean addAlliance returns nothing
    if (target == null) then
        call Trace("Attack target but have nothing\n")
        return
    endif
    if (addAlliance) then
        call Trace("Attack target with alliance target\n")
        call SetAllianceTarget(target)
    endif
    call Trace("Attack target form a group and move to kill\n")
    call FormGroup(3, true)
    call AttackMoveKillA(target)
    if (not addAlliance) then
        call SetAllianceTarget(null)
    endif
endfunction

function LaunchAttack takes nothing returns nothing
    local unit target = null
    local boolean setAlly = true

    call Trace("Launch attack\n")

    // Don't launch any attack while town is threatened
    if (TownThreatened()) then
        call Trace("Launch attack cancel since town is threatened\n")
        call Sleep(2)
        return
    endif

    call Trace("Launch attack enemy player\n")
    // Target Priority #1
    if (target == null) then
        set target = GetAllianceTarget()
        call Trace("Launch attack alliance target\n")
        if (target != null) then
            call Trace("Launch attack alliance target found\n")
            set setAlly = false
        endif
    endif

    // Target Priority #2
    if (target == null) then
        set target = GetExpansionFoe()
        call Trace("Launch attack expansion foe\n")
        if (target != null) then
            call Trace("Launch attack expansion foe found\n")
            set take_exp = false
        endif
    endif

    // Target Priority #3
    if (target == null) then
        call Trace("Launch attack mega target\n")
        set target = GetMegaTarget()
    endif

    // Target Priority #4
    if (target == null) then
        call Trace("Launch attack enemy expansion\n")
        set target = GetEnemyExpansion()
    endif

    // Target Priority #5
    if (target == null) then
        call Trace("Launch attack enemy base\n")
        call StartGetEnemyBase()
        loop
            exitwhen (not WaitGetEnemyBase())
            call SuicideSleep(1)
        endloop
        set target = GetEnemyBase()
    endif

    // Target Priority #6
    if (target == null) then
        call Trace("Launch attack creep camp small\n")
        set target = GetCreepCamp(0, 9, false)
    endif

    // Target Priority #7
    if (target == null) then
        call Trace("Launch attack creep camp big\n")
        set target = GetCreepCamp(0, 75, true)
    endif

    // Attack the target and increment attack wave
    if (target != null) then
        call Trace("Attack\n")
        call AttackTarget(target, setAlly)
    else
        call Trace("Wait for another attack try\n")
        // If no target was found, sleep a bit before trying again
        call Sleep(20)
    endif
endfunction

function main takes nothing returns nothing
    call InitHero()

    call CampaignAI(GERMANIC_HOUSE,null)
    call SetReplacements(9,9,9)
    call SetPeonsRepair(true)

    call SetBuildUnitEx( 1,1,1, GERMANIC_TIER_1    )
    call SetBuildUnitEx( 1,1,1, GERMANIC_ALTAR    )
    call SetBuildUnitEx( 1,1,1, GERMANIC_WATCH_TOWER    )
    call SetBuildUnitEx( 1,1,1, GERMANIC_STOCKPILE    )
    //call SetBuildUnitEx( 5,5,5, GERMAINC_WORKER         )


    call SetBuildUnitEx( 1,1,1, GERMANIC_TIER_2    )

    call SetBuildUpgr(1, UPG_GERMANIC_LUMBER)

    call SetBuildUnitEx( 1,1,1, GERMANIC_TIER_3    )

    call SetBuildUpgr(2, UPG_GERMANIC_LUMBER)

    loop
        call InitAssaultGroup()
        call CampaignAttackerEx(1, 1, 1, hero_id)
        call CampaignAttackerEx(3, 4, 4, GERMANIC_WARRIOR)
        call CampaignAttackerEx(1, 2, 3, GERMANIC_ARCHER)
        call CampaignAttackerEx(1, 2, 3, GERMANIC_RIDER)
        call Sleep(M1) // Waits 1 minute before attacking
        call LaunchAttack()
    endloop
endfunction
